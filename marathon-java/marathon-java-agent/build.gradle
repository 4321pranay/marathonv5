/*
Copyright 2016 Jalian Systems Pvt. Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

plugins {
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

configurations {
    antBuildXML
}

javadoc {
    exclude 'net/sourceforge/marathon/javaagent/Wait.java'
    exclude 'net/sourceforge/marathon/javaagent/server/Base64.java'
}

eclipse {
    jdt {
        sourceCompatibility = '1.6'
        targetCompatibility = '1.6'
        javaRuntimeName = 'JavaSE-1.6'

        file {
            withProperties { properties ->
                properties.setProperty('org.eclipse.jdt.core.compiler.problem.discouragedReference', 'ignore')
                properties.setProperty('org.eclipse.jdt.core.compiler.problem.forbiddenReference', 'ignore')
            }
        }
    }
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

compileJava {
    options.fork = true
    if(project.properties['jdk6home'])
        options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

compileTestJava {
    options.fork = true
    if(project.properties['jdk6home'])
     options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

dependencies {
    compile 'org.json:json:20180813'
    compile "org.nanohttpd:nanohttpd:${nanohttpd_version}"
    compile "org.javassist:javassist:${javassist_version}"
    implementation 'com.jidesoft:jide-oss:3.6.18'
    testCompile "org.testng:testng:${testng_version}"
    testCompile project(":marathon-java:marathon-test-helpers")
    antBuildXML 'org.json:json:20180813'
    antBuildXML "org.nanohttpd:nanohttpd:${nanohttpd_version}"
    antBuildXML "org.javassist:javassist:${javassist_version}"
}

jar {
    configurations.compile.each { from(zipTree(it.path).matching { exclude("META-INF/**") }) }
    manifest {
        attributes 'Manifest-Version': '1.0',
        'Agent-Class': 'net.sourceforge.marathon.javaagent.JavaAgentHook',
        'Can-Redefine-Classes': 'false',
        'Can-Retransform-Classes': 'false',
        'Premain-Class': 'net.sourceforge.marathon.javaagent.JavaAgentHook'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'marathon-java-agent'

            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Marathon Java/Swing Agent'
                description = 'Marathon JavaDriver agent for Java/Swing applications'
                url = 'https://marathontesting.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dakshinamurthy.karra'
                        name = 'Dakshinamurthy Karra'
                        email = 'dakshinamurthy.karra@jaliansystems.com'
                        organization = "Jalian Systems Pvt. Ltd."
                        organizationUrl = "http://jaliansystems.com"
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jalian-systems/marathonv5.git'
                    developerConnection = 'scm:git:git@github.com:jalian-systems/marathonv5.git'
                    url = 'https://github.com/jalian-systems/marathonv5'
                }
            }

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.each() {
                    it.parent().remove(it)
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

            credentials {
                username = mvnUser
                password = mvnPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useTestNG()
    systemProperty 'file.encoding', 'utf8'
    testLogging.showStandardStreams = true
}
