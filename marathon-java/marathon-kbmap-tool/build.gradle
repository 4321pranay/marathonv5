/*
Copyright 2016 Jalian Systems Pvt. Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

repositories {
  mavenCentral()
}

dependencies {
  compile "com.jgoodies:jgoodies-common:${jgoodies_common_version}"
  compile "com.jgoodies:jgoodies-forms:${jgoodies_forms_version}"
}

jar.doFirst{
  manifest {
    def classpath = configurations.runtime.filter{ it.name.startsWith 'marathon' }.collect { it.name } +
              configurations.runtime.filter{ !it.name.startsWith('marathon') }.collect { 'support/' + it.name }
    Properties props = new Properties()
    props.load(new FileInputStream(project(":marathon").file('build.properties')))
    props.put("marathonversion", "${version}")
    attributes("Manifest-Version" : "1.0",
              "Created-By" : props.get('blurbcompany'),
              "Specification-Title" : props.get('blurbtitle'),
              "Specification-Version" : props.get('marathonversion'),
              "Specification-Vendor" : props.get('blurbcompany'),
              "Implementation-Title" : props.get('marathonproduct'),
              "Implementation-Version" : props.get('marathonversion'),
              "Implementation-Vendor" : props.get('blurbcompany'),
              "Main-Class" : "net.sourceforge.marathon.kbmap.Generator",
              "Class-Path" : classpath.join(' ')
    )
  }
}

task startScripts(type: Copy) {
    destinationDir file("$buildDir/scripts")
    from('script-templates/marathon-kbmap-tool') {
      expand( [ 'marathonKBMapToolJar' : jar.archiveName, ])
      fileMode(0755)
    }
    from('script-templates/marathon-kbmap-tool.bat') {
      expand( [ 'marathonKBMapToolJar' : jar.archiveName, ])
    }
}

build.dependsOn(startScripts)
